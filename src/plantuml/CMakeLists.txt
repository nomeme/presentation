##
# @file This file contains the targets required to
# create images from plantuml sources.
##

message(STATUS "Please make sure wget is installed.")
message(STATUS "This component needs a java runtime.")

set(PLANTUML_BINARY_SOURCE "https://github.com/plantuml/plantuml/releases/download/v1.2024.7/plantuml-1.2024.7.jar")

set(PLANTUML_EXECUTABLE "plantuml.jar")

##
# Collect all of the Plantuml files in this folder.
##
file(GLOB PUML_FILES ${CMAKE_CURRENT_SOURCE_DIR}/*puml)
##
# Collect all of the ipuml files used for import.
##
file(GLOB IPUML_FILES ${CMAKE_CURRENT_SOURCE_DIR}/*ipuml)

add_custom_target(plantuml
    COMMENT "Create the plantuml files"
)

##
# Adds a target to clean the plantuml output.
##
add_custom_target(plantuml_clean
    COMMENT "Remove all generated plantuml files."
)

##
# Declaring the executable as source file here
# makes sure that we only download this when it is not
# available.
##
target_sources(plantuml
    PRIVATE
        ${CMAKE_CURRENT_BINARY_DIR}/${PLANTUML_EXECUTABLE}
)

foreach(file ${PUML_FILES})
    ##
    # We run plantuml for each *.puml file in this folder.
    #
    # Attention, This is not smart enough to resolve internal
    # plantuml includes and may require a cleanup call first
    # for complex dependencies.
    ##
    get_filename_component(file_name ${file} NAME_WE)
    add_custom_command(
        OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/${file_name}.svg
        COMMAND java -jar ${PLANTUML_EXECUTABLE} ${file} -tsvg -o ${CMAKE_CURRENT_BINARY_DIR}
        COMMENT "Run plantuml for ${file}"
        WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
        DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/${PLANTUML_EXECUTABLE}
        MAIN_DEPENDENCY ${file}
    )
    ##
    # This makes the command a dependency for the plantuml build.
    ##
    target_sources(plantuml
        PRIVATE
            ${CMAKE_CURRENT_BINARY_DIR}/${file_name}.svg
    )
    ##
    # Adds the possibility to clean the generated svg files.
    ##
    add_custom_command(TARGET plantuml_clean
        POST_BUILD
        COMMAND rm ${CMAKE_CURRENT_BINARY_DIR}/${file_name}.svg
        COMMENT "Remove ${file_name} generated by plantuml."
        WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
    )
endforeach(file IN PUML_FILES)


##
# Downloading plantuml executable.
# This downloads the GPL version of plantuml.
# Attention: This file is distributed under GPL license.
# Do not release or modify.
##
add_custom_command(
    COMMAND wget ${PLANTUML_BINARY_SOURCE} -O ${PLANTUML_EXECUTABLE}
    COMMENT "Download plantuml binaries."
    OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/${PLANTUML_EXECUTABLE}
    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
)

